#!/usr/bin/env perl6
use Whateverable;
use Misc;

unit class Nativecallable does Whateverable;

method help($msg) {
    “Like this {$msg.server.current-nick}: <some C definition>”;
}

sub run-gptrixie($header-file) {
    my %ENV = %*ENV.clone;
    %ENV<PATH> = join ‘:’, ‘/home/bisectable/.rakudobrew/bin’, %ENV<PATH>; # TODO
    my %output = get-output :%ENV, ‘gptrixie’, ‘--all’, ‘--silent’, $header-file;
    if %output<output>.lines > 20 {
        return ‘’ but FileStore(%(‘GPTrixiefied.pm6’ => "#Generated by App::GPTrixie\n" ~ %output<output>))
    }
    my @pruned-output;
    @pruned-output = %output<output>.lines.grep: { $_ and not .starts-with: ‘#’ };
    if @pruned-output ≤ 10 {
        return (@pruned-output.map: {.subst(/\s+/, " ", :g)}).join: “\n”;
    }
    my $definitive-output //= %output<output>;
    ‘’ but FileStore(%(‘GPTrixified.pm6’ => "#Generated by App::GPTrixie\n" ~ $definitive-output))
}

multi method irc-to-me($msg where /^ \s* $<code>=.+ /) {
    my $file = self.process-code: $<code>, $msg;
    my $code = slurp $file;
    $file.unlink;
    my $header-file = write-code “\n#include <stddef.h>\n#include <stdbool.h>\n” ~ $code;
    LEAVE unlink $_ with $header-file;
    run-gptrixie($header-file)
}


Nativecallable.new.selfrun: ‘nativecallable6’, [ / nativecall6? <before ‘:’> /,
                                                 fuzzy-nick(‘nativecallable6’, 2) ];
