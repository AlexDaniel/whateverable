my @bots = dir ‘bin’, test => / ‘able.p6’ $ /;

sub start-stop($task where ‘start-all’ | ‘stop-all’) {
    for @bots {
        next if .contains: ‘#’; # editor files
        my $bot = .basename.subst: /‘.p6’$/, ‘’;
        my $log = dir(‘logs’, test => /^ $($bot.lc) .* ‘.log’ $/).sort.tail;
        next unless $log; # TODO but what if there's no log?
        my $result = run :out, ‘lsof’, ‘-tac’, ‘moar’, ‘--’, $log;
        if $task eq ‘start-all’ {
            next if $result.exitcode == 0;
            say “Starting $bot…”;
            run ‘bash’, ‘-c’, ｢"./bin/$1.p6" &> "$(date "+logs/${1,,}_%F_%H%M.log")" & disown｣,
                 ‘--’, $bot, :env(|%*ENV, PERL6LIB => ‘lib’)
        } else { # ‘stop-all’
            next if $result.exitcode ≠ 0;
            say “Stopping $bot…”;
            run ‘kill’, ‘--’, $result.out.slurp-rest.chomp
        }
    }
}

task any(‘debug:’ X~ @bots».extension(‘’)».basename».lc), sub {
    # TODO https://github.com/perlpilot/p6-sake/issues/7
    my $bot = @*ARGS.grep({.starts-with: ‘debug:’})[0];
    return without $bot;
    $bot .= subst: /^‘debug:’/, ‘’;
    $bot .= tc;
    say “Starting $bot…”;
    run “./bin/$bot.p6”, :env(|%*ENV, PERL6LIB => ‘lib’, DEBUGGABLE => 1)
}

task ‘start-all’, { start-stop ‘start-all’ }
task ‘stop-all’,  { start-stop ‘stop-all’  }
task ‘test’,      { run ‘prove’, ‘-j’, ‘8’,
                        ‘--exec’, ‘perl6’, ‘t’ }

task ‘upgrade’ => ‘stop-all’, { run ‘rakudobrew’, ‘build’, ‘moar’ }

my $RSYNC = ‘bisectable@94.23.219.181:/home/bisectable/git/whateverable/’;
task ‘mothership-pull’, {
     run ‘rsync’, ‘--archive’, ‘--verbose’, ‘--fuzzy’, ‘--compress’,
         ‘--human-readable’,
         ‘--exclude=data’, ‘--exclude=.git’, ‘--exclude=.precomp’,
         $RSYNC, ‘.’
}

# vim: expandtab shiftwidth=4 ft=perl6
