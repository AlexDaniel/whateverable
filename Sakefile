my @bots = dir ‘bin’, test => / ‘able.p6’ $ /;

sub start-stop($task where ‘start-all’ | ‘stop-all’) {
    for @bots {
        next if .contains: ‘#’; # editor files
        my $bot = .basename.subst: /‘.p6’$/, ‘’;
        my $log = dir(‘logs’, test => /^ $($bot.lc) .* ‘.log’ $/).sort.tail;
        next unless $log; # TODO but what if there's no log?
        my $result = run :out, ‘lsof’, ‘-tac’, ‘moar’, ‘--’, $log;
        if $task eq ‘start-all’ {
            next if $result.exitcode == 0;
            say “Starting $bot…”;
            run ‘bash’, ‘-c’, ｢"./bin/$1.p6" &> "$(date "+logs/${1,,}_%F_%H%M.log")" & disown｣,
                 ‘--’, $bot, :env(|%*ENV, PERL6LIB => ‘lib’)
        } else { # ‘stop-all’
            next if $result.exitcode ≠ 0;
            say “Stopping $bot…”;
            run ‘kill’, ‘--’, $result.out.slurp-rest.chomp
        }
    }
}

task ‘start-all’, { start-stop ‘start-all’ }
task ‘stop-all’,  { start-stop ‘stop-all’  }
task ‘test’,      { run ‘prove’, ‘--exec’, ‘perl6’, ‘t’ }

task ‘upgrade’ => ‘stop-all’, { run ‘rakudobrew’, ‘build’, ‘moar’ }

# vim: expandtab shiftwidth=4 ft=perl6
