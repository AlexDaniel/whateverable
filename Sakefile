my @bots = dir ‘bin’, test => / ‘able.p6’ $ /;

task ‘debug:’ ~ @bots».extension(‘’)».basename».lc.any, sub {
    # TODO https://github.com/perlpilot/p6-sake/issues/7
    my $bot = @*ARGS.grep({.starts-with: ‘debug:’})[0];
    return without $bot;
    $bot .= subst: /^‘debug:’/, ‘’;
    $bot .= tc;
    say “Starting $bot…”;
    run “./bin/$bot.p6”, :env(|%*ENV, PERL6LIB => ‘lib’, DEBUGGABLE => 1)
}

task ‘start-all’, {
    say ‘start-all is no longer needed, ’
    ~ ‘systemd should start (and restart) the bots automatically’
}

sub kill-bot($bot) {
    say “Killing $bot…”;
    my $pid = run(:out, ‘systemctl’, ‘--property=MainPID’, ‘--’,
                  ‘show’, “whateverable@$bot.service”).out.slurp.trim;
    $pid .= subst: /^‘MainPID=’/, ‘’;
    run ‘kill’, ‘--’, $pid;
}

task ‘kill:’ ~ @bots».extension(‘’)».basename».lc.any, sub {
    # TODO https://github.com/perlpilot/p6-sake/issues/7
    my $bot = @*ARGS.grep({.starts-with: ‘kill:’})[0];
    return without $bot;
    $bot .= subst: /^‘kill:’/, ‘’;
    $bot .= tc;
    kill-bot $bot
}

task ‘kill-all’ | ‘stop-all’, { kill-bot $_ for @bots».extension(‘’)».basename }
task ‘test’, { run ‘prove’, ‘-j’, ‘8’, ‘--exec’, ‘perl6’, ‘t’ }
task ‘upgrade’ => ‘stop-all’, { run ‘rakudobrew’, ‘build’, ‘moar’ }

my $RSYNC = ‘bisectable@94.23.219.181:/home/bisectable/git/whateverable/’;
task ‘mothership-pull’, {
     run ‘rsync’, ‘--archive’, ‘--verbose’, ‘--fuzzy’, ‘--compress’,
         ‘--human-readable’,
         ‘--exclude=data’, ‘--exclude=.git’, ‘--exclude=.precomp’,
         $RSYNC, ‘.’;
     True
}

# vim: expandtab shiftwidth=4 ft=perl6
